
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:
	Logger log = Logger.getLogger(getClass());

	public void report_fatal_error(String message, Object info) throws java.lang.Exception {
		done_parsing(); report_error(message, info); }

	public void syntax_error(Symbol curr_token) {
		report_error("\nSintaksna greska", curr_token); }

	public void unrecovered_syntax_error(Symbol curr_token) throws java.lang.Exception {
		report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", curr_token); }

	public void report_error(String message, Object info) {
		StringBuilder msg = new StringBuilder(message);
		if (info instanceof Symbol)
			msg.append(" na liniji ").append(((Symbol)info).left);
		log.error(msg.toString());
	}
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, LBRACE, RBRACE, SEMI, LPAREN, RPAREN, EQUAL, RETURN;
terminal DOUBLEMINUS, DOUBLECOLON, READ, NEW, COMMA, DOUBLEPLUS;
terminal VOID, PRINT;
terminal LBRACKET, CONST, RBRACKET, NAMESPACE;
terminal Integer NUMBER;
terminal String IDENT, CHARACTER, BOOLEAN;
terminal String PLUS, MINUS, MUL, DIV, PER;

nonterminal VarDeclList, MethodDeclList, VarDecl;
nonterminal StatementList, Statement, DeclList;
nonterminal ConstDeclList, ProgNameDeclList;
nonterminal rs.etf.pp1.symboltable.concepts.Obj NamespaceDecl, NamespaceName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, SimpleDesignator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl, MethodName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ArrayDesignator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Addop, Mulop, Identifier;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Factor, Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Initializator, ConstIdentifier;


Program ::= (Prog) PROG ProgName:p ProgNameDeclList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

ProgNameDeclList ::= ProgNameDeclList NamespaceDecl
					| ProgNameDeclList VarDecl
					| /* epsilon */;

NamespaceDecl ::= (NamespDecl) NAMESPACE NamespaceName LBRACE VarDeclList LBRACE RBRACE RBRACE;

NamespaceName ::= (NamespName) IDENT:i;

VarDeclList ::= VarDeclList VarDecl
			| /* epsilon */;

VarDecl ::= (DeclListType) Type:varType DeclList SEMI
		| (ConstDeclListType) CONST Type:varType ConstDeclList SEMI
		| error SEMI:l
		{:
			parser.report_error(
				"Izvrsen oporavak do ; u deklaraciji promenljivih na liniji "
				+ lleft, null
			);
		:};

DeclList ::= Identifier COMMA DeclList
			| Identifier;

Identifier ::= (SIdentDecl) IDENT
			| (AIdentDecl) IDENT LBRACKET RBRACKET
			| (IIdentDecl) IDENT EQUAL Initializator
			| error
			{:
				parser.report_error(
					"Izvrsen oporavak do ,(|;) u deklaraciji promenljivih",
					null
				);
			:};

ConstDeclList ::= ConstIdentifier COMMA ConstDeclList
				| ConstIdentifier;

ConstIdentifier ::= (CIdentDecl) IDENT EQUAL Initializator
				| error
				{:
					parser.report_error(
						"Izvrsen oporavak do ,(|;) u deklaraciji promenljivih",
						null
					);
				:};
			
Initializator ::= (NInit) NUMBER | (CInit) CHARACTER | (BInit) BOOLEAN;

Type ::= (TypeAccess) IDENT:typeName;

MethodDeclList ::= MethodDeclList MethodDecl
				| /* epsilon */;

MethodDecl ::= (MethodDeclaration) VOID MethodName LPAREN RPAREN VarDeclList LBRACE StatementList RBRACE;

MethodName ::= (MethName) IDENT;

StatementList ::= StatementList Statement
				| /* epsilon */;

Statement ::= (SDesignAsign) SimpleDesignator EQUAL Expr:e SEMI
			| (ADesignAsign) ArrayDesignator EQUAL Expr:e SEMI
			| (ArrayAlloc) SimpleDesignator EQUAL NEW Type LBRACKET Expr RBRACKET SEMI
			| error SEMI:l
			{:
				parser.report_error(
					"Izvrsen oporavak do ; na liniji "
					+ lleft, null
				);
			:}
			| (SDesignInc) SimpleDesignator DOUBLEPLUS SEMI
			| (ADesignInc) ArrayDesignator DOUBLEPLUS SEMI
			| (SDesignDec) SimpleDesignator DOUBLEMINUS SEMI
			| (ADesignDec) ArrayDesignator DOUBLEMINUS SEMI
			| READ LPAREN SimpleDesignator RPAREN SEMI
			| READ LPAREN ArrayDesignator RPAREN SEMI
			| (PrintStmt) PRINT LPAREN Expr RPAREN SEMI
			| (PrintOnWith) PRINT LPAREN Expr COMMA NUMBER RPAREN SEMI
			| RETURN SEMI;

Expr ::= (AddExpr) Expr Addop Term
		| (NegExpr) MINUS Term
		| (TermExpr) Term;

Term ::= (MulTerm) Term:t Mulop Factor:te
		| (FactorTerm) Factor:t;

Factor ::= (NumberConst) NUMBER
		| (CharacterConst) CHARACTER
		| (BooleanConst) BOOLEAN
		| (ParenExpr) LPAREN Expr RPAREN
		| (SFactorDesignator) SimpleDesignator:d
		| (AFactorDesignator) ArrayDesignator:d;

SimpleDesignator ::= (Ident) IDENT:name
					| (NsIdent) IDENT:namesp DOUBLECOLON IDENT:name;

ArrayDesignator ::= (ArrIdent) IDENT:name LBRACKET Expr RBRACKET
				| (NsArrIdent) IDENT:namesp DOUBLECOLON IDENT:name LBRACKET Expr RBRACKET;

Mulop ::= (Mul) MUL | (Div) DIV | (Per) PER;

Addop ::= (Plus) PLUS | (Minus) MINUS;